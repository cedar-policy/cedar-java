buildscript {
  repositories {
    maven {
      url "https://plugins.gradle.org/m2/"
    }
  }
  dependencies {
    classpath "com.github.spotbugs.snom:spotbugs-gradle-plugin:5.0.14"
    classpath "gradle.plugin.com.github.sherter.google-java-format:google-java-format-gradle-plugin:0.9"
  }
}

/*
 Applies core Gradle plugins, which are ones built into Gradle itself.
*/
plugins {
    // Java for compile and unit test of Java source files. Read more at:
    // https://docs.gradle.org/current/userguide/java_plugin.html
    id 'java'

    // JaCoCo for coverage metrics and reports of Java source files. Read more at:
    // https://docs.gradle.org/current/userguide/jacoco_plugin.html
    id 'jacoco'

    // For publishing
    id 'maven-publish'
    id 'signing'
}

/*
 Applies community Gradle plugins, usually added as build-tools in Config.
*/

// SpotBugs for quality checks and reports of source files. Read more at:
// https://spotbugs.readthedocs.io/en/stable/gradle.html
apply plugin: 'com.github.spotbugs'

/*
 Configures the JaCoCo "jacoco" plugin. Remove this if you want to skip
 these checks and report generation.

 Set minimum code coverage to fail build, where 0.01 = 1%.
*/
check.dependsOn jacocoTestCoverageVerification
jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 0.2
            }
        }
    }
}

/*
 Configures the SpotBugs "com.github.spotbugs" plugin. Remove this and the
 plugin to skip these checks and report generation.
*/
spotbugs {
    ignoreFailures.set(false)
}

repositories {
    mavenCentral()
}

dependencies {
    // Do not upgrade to Jackson 3.x without addressing stack overflow issues in ValueCedarDeserializer
    // The upgrade should be reviewed by AppSec
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.15.0'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jdk8:2.15.0'
    implementation 'org.slf4j:slf4j-api:2.0.7'
    implementation 'org.apache.commons:commons-text:1.10.0'
    implementation 'com.google.code.findbugs:findbugs:3.0.1'
    implementation 'org.apache.logging.log4j:log4j-core:2.20.0'
    implementation 'org.apache.logging.log4j:log4j-to-slf4j:2.20.0'
    implementation 'com.google.guava:guava:31.0.1-jre'

    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.9.3'
    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.9.3'
    testImplementation 'org.mockito:mockito-inline:5.2.0'
    testImplementation 'net.jqwik:jqwik:1.7.3'
    testImplementation 'org.slf4j:slf4j-reload4j:2.0.7'
}

test {
    useJUnitPlatform()
    //environment "CEDAR_INTEGRATION_TESTS_ROOT", ''set to absolute path of `cedar-integration-tests`'
    //environment 'CEDAR_JAVA_FFI_LIB', 'set to absolute path of cedar_java_ffi native library (including file extension)'
    testLogging {
        showStandardStreams false
        exceptionFormat 'full'
    }
}

java {
    withSourcesJar()
    withJavadocJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            groupId = 'com.cedarpolicy'
            artifactId = 'cedar-java'
            version = '2.3.3'
            pom {
                name = 'CedarJava'
                description = 'Java bindings for Cedar policy language.'
                url = 'https://www.cedarpolicy.com'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'cedar'
                        name = 'Cedar Team'
                        email = 'cedar-sonatype-team@amazon.com'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/cedar-policy/cedar-java.git'
                    developerConnection = 'scm:git:https://github.com/cedar-policy/cedar-java.git'
                    url = 'https://github.com/cedar-policy/cedar-java'
                }
            }
        }
    }
    repositories {
        maven {
            url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username ossrhUsername
                password ossrhPassword
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}